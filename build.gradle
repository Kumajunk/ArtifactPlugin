plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "9.0.0-beta4"
}

group = 'io.github.itokagimaru'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven{ url = "https://jitpack.io" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    compileOnly files("libs/MythicMobs-5.11.1.jar")
    compileOnly files("libs/Magic-10.10.3.jar")

    compileOnly("com.github.MilkBowl:VaultAPI:1.7.1")
    implementation("com.zaxxer:HikariCP:5.1.0")
    implementation("com.mysql:mysql-connector-j:9.5.0")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.8")
    }

    shadowJar {
        // HikariCPとMySQLドライバーをリロケート（パッケージ競合を避けるため）
        relocate("com.zaxxer.hikari", "io.github.itokagimaru.artifact.libs.hikari")
        relocate("com.mysql", "io.github.itokagimaru.artifact.libs.mysql")
        
        // 出力ファイル名を設定
        archiveClassifier.set("")
    }

    // 通常のjarタスクの代わりにshadowJarを使用
    build {
        dependsOn(shadowJar)
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
